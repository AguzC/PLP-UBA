--- ---
--- title: PrÃ¡ctica 1 - ProgramaciÃ³n funcional
--- author: Ernesto Ittig
--- date: 2025-03-27
--- ---
{-# LANGUAGE LambdaCase #-}

module Ejercicio.PLP.Practica01
where

import Data.List (nub)

--- ## CurrificaciÃ³n y tipos

--- ### Ejercicio 1

--- 1. _Â¿CuÃ¡l es el tipo de cada funciÃ³n?_

max2 :: (Ord a) => (a, a) -> a
max2 (x, y)
  | x >= y = x
  | otherwise = y

normaVectorial :: (Float, Float) -> Float
normaVectorial (x, y) = sqrt (x ^ 2 + y ^ 2)

subtract_ :: (Num a) => a -> a -> a
subtract_ = flip (-)

predecesor :: (Num a) => a -> a
predecesor = subtract_ 1

evaluarEnCero :: (Num a) => (a -> b) -> b
evaluarEnCero f = f 0

dosVeces :: (a -> a) -> a -> a
dosVeces f = f . f

flipAll :: [b -> a -> c] -> [a -> b -> c]
flipAll = map flip

flipRaro :: a -> (b -> a -> c) -> b -> c
flipRaro = flip flip

--- 2. _Indicar cuÃ¡les de las funciones anteriores no estÃ¡n currificadas. Para
---    cada una de ellas, definir la funciÃ³n currificada correspondiente_

max2' :: (Ord a) => a -> a -> a
max2' x y
  | x >= y = x
  | otherwise = y

normaVectorial' :: Float -> Float -> Float
normaVectorial' x y = sqrt (x ^ 2 + y ^ 2)

--- ### Ejercicio 2
--- 1. _Definir la funciÃ³n `curry`, que dada una funciÃ³n de dos argumentos,
---    devuelve su equivalente currificada._

curry_ :: ((a, b) -> c) -> a -> b -> c
curry_ f x y = f (x, y)

--- 2. _Definir la funciÃ³n `uncurry`, que dada una funciÃ³n currificada de dos
---    argumentos, devuelve su versiÃ³n no currificada equivalente. Es la inversa
---    de la anterior._

uncurry_ :: (a -> b -> c) -> (a, b) -> c
uncurry_ f (x, y) = f x y

--- 3. _Se podrÃ­a definir una funciÃ³n `curryN`, que tome una funciÃ³n de un
---    nÃºmero arbitrario de argumentos y devuelva su versiÃ³n currificada?_
---
---    No se puede con el sistema de tipos de Haskell.

--- ## Esquemas de recursiÃ³n

--- ### Ejercicio 3

--- 1. _Redefinir usando foldr las funciones `sum`, `elem`, `(++)`, `filter` y
---   `map`._

sum_ :: (Num a) => [a] -> a
sum_ = foldr (+) 0

elem_ :: (Eq a) => a -> [a] -> Bool
elem_ x = foldr ((||) . (x ==)) False

(+++) :: [a] -> [a] -> [a]
(+++) = flip $ foldr (:)

filter_ :: (a -> Bool) -> [a] -> [a]
filter_ p = foldr (\x r -> if p x then x : r else r) []

map_ :: (a -> b) -> [a] -> [b]
map_ f = foldr ((:) . f) []

--- 2. _Definir la funciÃ³n `mejorSegÃºn :: (a -> a -> Bool) -> [a] -> a`, que
---    devuelve el mÃ¡ximo elemento de la lista segÃºn una funciÃ³n de comparaciÃ³n,
---    utilizando `foldr1`. Por ejemplo, `maximum = mejorSegÃºn (>)`._

mejorSegun :: (a -> a -> Bool) -> [a] -> a
mejorSegun p = foldr1 (\x y -> if p x y then x else y)

--- 3. _Definir la funciÃ³n `sumasParciales :: Num a => [a] -> [a]`, que dada una
---    lista de nÃºmeros devuelve otra de la misma longitud, que tiene en cada
---    posiciÃ³n la suma parcial de los elementos de la lista original desde la
---    cabeza hasta la posiciÃ³n actual. Por ejemplo,
---    `sumasParciales [1,4,-1,0,5] â†’ [1,5,4,4,9]`._

sumasParciales :: Num a => [a] -> [a]
sumasParciales = scanl1 (+)

--- 4. _Definir la funciÃ³n `sumaAlt`, que realiza la suma alternada de los
---    elementos de una lista. Es decir, da como resultado: el primer elemento,
---    menos el segundo, mÃ¡s el tercero, menos el cuarto, etc. Usar `foldr`._

sumaAlt :: Num a => [a] -> a
sumaAlt = foldr (-) 0

--- 5. _Hacer lo mismo que en el punto anterior, pero en sentido inverso (el
---    Ãºltimo elemento menos el anteÃºltimo, etc.). Pensar quÃ© esquema de
---    recursiÃ³n conviene usar en este caso._

sumaAlt' :: Num a => [a] -> a
sumaAlt' = foldl (-) 0

--- ### Ejercicio 4

--- 1. _Definir la funciÃ³n `permutaciones :: [a] -> [[a]]`, que dada una lista
---    devuelve todas sus permutaciones_

permutaciones :: [a] -> [[a]]
permutaciones = foldr (concatMap . intercambiar) [[]]
 where
  intercambiar v xs = [take n xs ++ [v] ++ drop n xs | n <- [0 .. (length xs)]]

--- 2. _Definir la funciÃ³n partes, que recibe una lista L y devuelve la lista de
---    todas las listas formadas por los mismos elementos de L, en su mismo
---    orden de apariciÃ³n._

partes :: [a] -> [[a]]
partes = foldr (concatMap . (\x xs -> [xs, x : xs])) [[]]

--- 3. _Definir la funciÃ³n prefijos, que dada una lista, devuelve todos sus
---    prefijos._

prefijos :: [a] -> [[a]]
prefijos xs = [take n xs | n <- [0 .. (length xs)]]

--- 4. _Definir la funciÃ³n sublistas que, dada una lista, devuelve todas sus
---    sublistas (listas de elementos que aparecen consecutivos en la lista
---    original)._

sublistas :: [a] -> [[a]]
sublistas ls = [] : [take n . drop m $ ls | n <- [1 .. t], m <- [0 .. (t - n)]]
 where
  t = length ls

--- ### Ejercicio 5

--- _Considerar las siguientes funciones:_

--- _Indicar si la recursiÃ³n utilizada en cada una de ellas es o no estructural.
--- Si lo es, reescribirla utilizando foldr. En caso contrario, explicar el
--- motivo._

elementosEnPosicionesPares :: [a] -> [a]
elementosEnPosicionesPares [] = []
elementosEnPosicionesPares (x : xs) =
  if null xs
    then [x]
    else x : elementosEnPosicionesPares (tail xs)

--- No es estructural, porque la funciÃ³n opera y decide sobre el valor de `xs`.

entrelazar :: [a] -> [a] -> [a]
entrelazar [] = id
entrelazar (x : xs) = \ys ->
  if null ys
    then x : entrelazar xs []
    else x : head ys : entrelazar xs (tail ys)

--- Es recursiÃ³n estructural.

--- ### Ejercicio 6

--- _El siguiente esquema captura la recursiÃ³n primitiva sobre listas._

recr :: (a -> [a] -> b -> b) -> b -> [a] -> b
recr _ z [] = z
recr f z (x : xs) = f x xs (recr f z xs)

--- a) _Definir la funciÃ³n `sacarUna :: Eq a => a -> [a] -> [a]`, que dados un
---    elemento y una lista devuelve el resultado de eliminar de la lista la
---    primera apariciÃ³n del elemento (si estÃ¡ presente)._

sacarUna :: Eq a => a -> [a] -> [a]
sacarUna v = recr (\x xs rec -> if x == v then xs else x : rec) []

--- b) _Explicar por quÃ© el esquema de recursiÃ³n estructural (`foldr`) no es
---    adecuado para implementar la funciÃ³n `sacarUna` del punto anterior._

---    Cuando se encuentra la primera instancia del carÃ¡cter a sacar, se debe
---    detener la recursiÃ³n y devolver la cola de la lista. Para hacer eso se
---    debe tener acceso a la cola de la lista, que `foldr` no proporciona.

--- c) Definir la funciÃ³n `insertarOrdenado :: Ord a => a -> [a] -> [a]` que
---    inserta un elemento en una lista ordenada (de manera creciente), de
---    manera que se preserva el ordenamiento

insertarOrdenado :: Ord a => a -> [a] -> [a]
insertarOrdenado v = recr (\x xs rec -> if x > v then v : x : xs else x : rec) []

--- ### Ejercicio 7

--- _Definir las siguientes funciones para trabajar sobre listas, y dar su tipo.
--- Todas ellas deben poder aplicarse a listas *finitas* e *infinitas*._

--- i) _`mapPares`, una versiÃ³n de `map` que toma una funciÃ³n currificada de dos
---    argumentos y una lista de pares de valores, y devuelve la lista de
---    aplicaciones de la funciÃ³n a cada par._

mapPares :: (a -> b -> c) -> [(a, b)] -> [c]
mapPares f = map (uncurry f)

--- ii) `armarPares`, que dadas dos listas arma una lista de pares que contiene,
---     en cada posiciÃ³n, el elemento correspondiente a esa posiciÃ³n en cada una
---     de las listas. Si una de las listas es mÃ¡s larga que la otra, ignorar
---     los elementos que sobran (el resultado tendrÃ¡ la longitud de la lista
---     mÃ¡s corta). Esta funciÃ³n en Haskell se llama `zip`.

armarPares :: [a] -> [b] -> [(a, b)]
armarPares = foldr sig (const [])
 where
  sig x rec [] = []
  sig x rec (y : ys) = (x, y) : rec ys

--- iii) `mapDoble`, una variante de `mapPares`, que toma una funciÃ³n
---      currificada de dos argumentos y dos listas (de igual longitud), y
---      devuelve una lista de aplicaciones de la funciÃ³n a cada elemento
---      correspondiente de las dos listas. Esta funciÃ³n en Haskell se llama
---      `zipWith`.

mapDoble :: (a -> b -> c) -> [a] -> [b] -> [c]
mapDoble f = (mapPares f .) . armarPares

--- ### Ejercicio 8

--- i) _Escribir la funciÃ³n `sumaMat`, que representa la suma de matrices, usando
---    `zipWith`. Representaremos una matriz como la lista de sus filas. Esto
---    quiere decir que cada matriz serÃ¡ una lista finita de listas finitas,
---    todas de la misma longitud, con elementos enteros. Recordamos que la suma
---    de matrices se define como la suma celda a celda. Asumir que las dos
---    matrices a sumar estÃ¡n bien formadas y tienen las mismas dimensiones._

sumaMat :: [[Int]] -> [[Int]] -> [[Int]]
sumaMat = zipWith (zipWith (+))

--- ii) _Escribir la funciÃ³n `trasponer`, que, dada una matriz como las del Ã­tem
---     $i$, devuelva su traspuesta. Es decir, en la posiciÃ³n $i, j$ del
---     resultado estÃ¡ el contenido de la posiciÃ³n $j, i$ de la matriz original.
---     Notar que si la entrada es una lista de $N$ listas, todas de longitud
---     $M$ , la salida debe tener $M$ listas, todas de longitud $N$._

transponer :: [[Int]] -> [[Int]]
transponer = foldr sacarP []
 where
  sacarP xs = zipWith (:) xs . fillN (length xs)
  fillN n xs = xs ++ replicate (n - length xs) []

--- ### Ejercicio 9

--- i) _Definir y dar el tipo del esquema de recursiÃ³n `foldNat` sobre los
---    naturales. Utilizar el tipo `Integer` de Haskell (la funciÃ³n va a estar
---    definida sÃ³lo para los enteros mayores o iguales que 0)._

foldNat :: b -> (b -> b) -> Integer -> b
foldNat fCero fSuc n =
  case n of
    0 -> fCero
    n -> fSuc (foldNat fCero fSuc (n - 1))

--- ii) _Utilizando `foldNat`, definir la funciÃ³n potencia._

potencia :: Integer -> Integer -> Integer
potencia n = foldNat 1 (* n)

--- ### Ejercicio 10

--- i) _Definir la funciÃ³n `genLista :: a -> (a -> a) -> Integer -> [a]`, que genera
---    una lista de una cantidad dada de elementos, a partir de un elemento
---    inicial y de una funciÃ³n de incremento entre los elementos de la lista.
---    Dicha funciÃ³n de incremento, dado un elemento de la lista, devuelve el
---    elemento siguiente._

genLista :: a -> (a -> a) -> Integer -> [a]
genLista z f n = scanl (flip (const f)) z [1 .. n]

--- ii) _Usando `genLista`, definir la funciÃ³n `desdeHasta`, que dado un par de
---     nÃºmeros (el primero menor que el segundo), devuelve una lista de nÃºmeros
---     consecutivos desde el primero hasta el segundo._
desdeHasta :: (Integer, Integer) -> [Integer]
desdeHasta (x, y) = genLista x (+ 1) (y - x)

--- ### Ejercicio 11

--- _Definir el esquema de recursiÃ³n estructural para el siguiente tipo:_

data Polinomio a
  = X
  | Cte a
  | Suma (Polinomio a) (Polinomio a)
  | Prod (Polinomio a) (Polinomio a)

--- _Luego usar el esquema definido para escribir la funciÃ³n
--- `evaluar :: Num a => a -> Polinomio a -> a` que, dado un nÃºmero y un
--- polinomio, devuelve el resultado de evaluar el polinomio dado en el nÃºmero
--- dado._

foldPol :: b -> (a -> b) -> (b -> b -> b) -> (b -> b -> b) -> Polinomio a -> b
foldPol fX fCte fSuma fProd pol =
  case pol of
    X -> fX
    Cte v -> fCte v
    Suma p1 p2 -> fSuma (rec p1) (rec p2)
    Prod p1 p2 -> fProd (rec p1) (rec p2)
 where
  rec = foldPol fX fCte fSuma fProd

evaluar :: Num a => a -> Polinomio a -> a
evaluar x = foldPol x id (+) (*)

--- ### Ejercicio 12

--- _Considerar el siguiente tipo, que representa a los Ã¡rboles binarios:_

data AB a = Nil | Bin (AB a) a (AB a)
  deriving (Show)

--- i) _Usando recursiÃ³n explÃ­cita, definir los esquemas de recursiÃ³n estructural
---    (foldAB) y primitiva (recAB), y dar sus tipos._

foldAB :: b -> (b -> a -> b -> b) -> AB a -> b
foldAB fNil fBin tree =
  case tree of
    Nil -> fNil
    Bin l x r -> fBin (rec l) x (rec r)
 where
  rec = foldAB fNil fBin

recAB :: b -> (AB a -> a -> AB a -> b -> b -> b) -> AB a -> b
recAB fNil fBin tree =
  case tree of
    Nil -> fNil
    Bin l x r -> fBin l x r (rec l) (rec r)
 where
  rec = recAB fNil fBin

--- ii) _Definir las funciones `esNil`, `altura` y `cantNodos`_

esNil :: AB a -> Bool
esNil = \case Nil -> True; _ -> False

altura :: AB a -> Int
altura = foldAB 0 (\l _ v -> 1 + max l v)

cantNodos :: AB a -> Int
cantNodos = foldAB 0 (\l _ v -> 1 + l + v)

--- iii) _Definir la funciÃ³n `mejorSegÃºn :: (a -> a -> Bool) -> AB a -> a,`
---      anÃ¡loga a la del ejercicio 3, para Ã¡rboles. Se recomienda definir una
---      funciÃ³n auxiliar para comparar la raÃ­z con un posible resultado de la
---      recursiÃ³n para un Ã¡rbol que puede o no ser `Nil`._

mejorSegunAB :: (a -> a -> Bool) -> AB a -> a
mejorSegunAB p t = case foldAB Nothing elegir t of Just x -> x
 where
  elegir l x r = max' (Just x) (max' l r)
  max' = maybe id (\x -> fmap $ \y -> if p x y then x else y)

--- iv) _Definir la funciÃ³n `esABB :: Ord a => AB a -> Bool` que chequea si un
---     Ã¡rbol es un Ã¡rbol binario de bÃºsqueda. Recordar que, en un Ã¡rbol binario
---     de bÃºsqueda, el valor de un nodo es mayor o igual que los valores que
---     aparecen en el subÃ¡rbol izquierdo y es estrictamente menor que los
---     valores que aparecen en el subÃ¡rbol derecho._
esABB :: Ord a => AB a -> Bool
esABB =
  recAB
    True
    (\l x r recL recR -> recL && recR && mejor (>=) x l && mejor (<) x r)
 where
  mejor f x = \case Nil -> True; t -> f (mejorSegunAB f t) x

--- v) _Justificar la elecciÃ³n de los esquemas de recursiÃ³n utilizados para los
---    tres puntos anteriores._

--- UsÃ© recursiÃ³n estructural siempre que pude. En `esABB` necesitaba poder
--- acceder a todos los hijos de un nodo para ver que se cumpla la condiciÃ³n. ğŸ‘

--- ### Ejercicio 13

--- _Dado el tipo `AB a` del ejercicio 12:_

--- i) _Definir las funciones `ramas` (caminos desde la raÃ­z hasta las hojas),
---    `cantHojas` y `espejo`._

ramas :: AB a -> [[a]]
ramas =
  foldAB
    []
    ( \l x r -> case l ++ r of
        [] -> [[x]]
        xs -> map (x :) xs
    )

cantHojas :: AB a -> Integer
cantHojas = foldAB 0 (\l x r -> case l + r of 0 -> 1; v -> v)

espejo :: AB a -> AB a
espejo = foldAB Nil (\l x r -> Bin r x l)

--- ii) _Definir la funciÃ³n `mismaEstructura :: AB a -> AB b -> Bool` que, dados
---     dos Ã¡rboles, indica si Ã©stos tienen la misma forma, independientemente
---     del contenido de sus nodos. Pista: usar evaluaciÃ³n parcial y recordar el
---     ejercicio 7._

mismaEstructura :: AB a -> AB b -> Bool
mismaEstructura =
  foldAB
    esNil
    ( \l _ r -> \case
        Nil -> False
        Bin l' _ r' -> l l' && r r'
    )

--- ### Ejercicio 14

--- _Se desea modelar en Haskell los Ã¡rboles con informaciÃ³n en las hojas (y
--- sÃ³lo en ellas). Para esto introduciremos el siguiente tipo:_

data AIH a = Hoja a | Bin' (AIH a) (AIH a)
  deriving (Show)

--- a) _Definir el esquema de recursiÃ³n estructural `foldAIH` y dar su tipo. Por
---    tratarse del primer esquema de recursiÃ³n que tenemos para este tipo, se
---    permite usar recursiÃ³n explÃ­cita._

foldAIH :: (a -> b) -> (b -> b -> b) -> AIH a -> b
foldAIH fHoja fBin = \case
  Hoja a -> fHoja a
  Bin' l r -> fBin (rec l) (rec r)
 where
  rec = foldAIH fHoja fBin

--- b) _Escribir las funciones `altura :: AIH a -> Integer` y `tamaÃ±o :: AIH a -> Integer`._
---    Considerar que la altura de una hoja es 1 y el tamaÃ±o de un AIH es su cantidad de hojas.
alturaAIH :: AIH a -> Integer
alturaAIH = foldAIH (const 1) (((+ 1) .) . max)

tamaÃ±o :: AIH a -> Integer
tamaÃ±o = foldAIH (const 1) (+)

--- ### Ejercicio 15

--- i) _Definir el tipo `RoseTree` de Ã¡rboles no vacÃ­os, con una cantidad
---    indeterminada de hijos para cada nodo._

data RoseTree a = Rose a [RoseTree a]

--- ii) _Escribir el esquema de recursiÃ³n estructural para `RoseTree`.
---     Importante escribir primero su tipo._

foldRose :: (a -> [b] -> b) -> RoseTree a -> b
foldRose fRose (Rose x r) = fRose x (map (foldRose fRose) r)

--- iii) _Usando el esquema definido, escribir las siguientes funciones:_
---      a) _`hojas`, que dado un `RoseTree`, devuelva una lista con sus hojas
---         ordenadas de izquierda a derecha, segÃºn su apariciÃ³n en el `RoseTree`_
---      b) _`distancias`, que dado un `RoseTree`, devuelva las distancias de su
---         raÃ­z a cada una de sus hojas._
---      c) _`altura`, que devuelve la altura de un RoseTree (la cantidad de
---         nodos de la rama mÃ¡s larga). Si el `RoseTree` es una hoja, se
---         considera que su altura es 1._

hojas :: RoseTree a -> [a]
hojas = foldRose (\v -> \case [] -> [v]; rss -> concat rss)

distancias :: RoseTree a -> [Integer]
distancias = foldRose (const $ \case [] -> [1]; rss -> map (+ 1) . concat $ rss)

alturaRT :: RoseTree a -> Integer
alturaRT = foldRose (const $ \case [] -> 1; rss -> 1 + maximum rss)

--- ### Ejercicio 16

--- Se desea representar conjuntos mediante Hashing abierto (chain addressing).
--- El Hashing abierto consta de dos funciones: una funciÃ³n de Hash, que dado un
--- elemento devuelve un valor entero (el cual se espera que no se
--- repita con frecuencia), y una tabla de Hash, que dado un nÃºmero entero devuelve
--- los elementos del conjunto a los que la funciÃ³n de Hash asignÃ³ dicho nÃºmero (es
--- decir, la preimagen de la funciÃ³n de Hash para ese nÃºmero).

--- Los representaremos en Haskell de la siguiente manera:

data HashSet a = Hash (a -> Integer) (Integer -> [a])

--- Por contexto de uso, vamos a suponer que la tabla de Hash es una funciÃ³n total,
--- que devuelve listas vacÃ­as para los nÃºmeros que no corresponden a elementos
--- del conjunto. Este es un invariante que deberÃ¡ preservarse en todas las
--- funciones que devuelvan conjuntos.

--- _Definir las siguientes funciones:_

--- i) _`vacÃ­o :: (a -> Integer) -> HashSet a`, que devuelve un conjunto vacÃ­o
---    con la funciÃ³n de Hash indicada._

vacio :: (a -> Integer) -> HashSet a
vacio f = Hash f (return [])

--- ii) _`pertenece :: Eq a => a -> HashSet a -> Bool`, que indica si un elemento
---     pertenece a un conjunto. Es decir, si se encuentra en la lista obtenida
---     en la tabla de Hash para el nÃºmero correspondiente a la funciÃ³n de Hash
---     del elemento._

pertenece :: Eq a => a -> HashSet a -> Bool
pertenece x (Hash f g) = x `elem` (g . f) x

--- iii) _`agregar :: Eq a => a -> HashSet a -> HashSet a`, que agrega un
---      elemento a un conjunto. Si el elemento ya estaba en el conjunto, se debe
---      devolver el conjunto sin modificaciones._

agregar :: Eq a => a -> HashSet a -> HashSet a
agregar x h@(Hash f g)
  | pertenece x h = h
  | otherwise = Hash f (\x' -> if x' == f x then x : g x' else g x')

--- iv) _`intersecciÃ³n :: Eq a => HashSet a -> HashSet a -> HashSet a` que,
---     dados dos conjuntos, devuelve un conjunto con la misma funciÃ³n de Hash
---     del primero y con los elementos que pertenecen a ambos conjuntos a la vez._

intersecciÃ³n :: Eq a => HashSet a -> HashSet a -> HashSet a
intersecciÃ³n (Hash f g1) (Hash _ g2) = Hash f (\x -> nub $ g1 x ++ g2 x)

--- v) _`foldr1` (no relacionada con los conjuntos). Dar el tipo y definir la
---    funciÃ³n foldr1 para listas sin usar recursiÃ³n explÃ­cita, recurriendo a
---    alguno de los esquemas de recursiÃ³n conocidos. Se recomienda usar la
---    funciÃ³n `error :: String -> a` para el caso de la lista vacÃ­a._

foldr1_ :: (a -> a -> a) -> [a] -> a
foldr1_ _ [] = error "lista vacÃ­a :("
foldr1_ f xs = foldr f (last xs) xs

--- ## GeneraciÃ³n infinita

--- ### Ejercicio 17

--- _Â¿CuÃ¡l es el valor de esta expresiÃ³n?_

[1, 3] =
  [x | x <- [1 .. 3], y <- [x .. 3], (x + y) `mod` 3 == 0]

--- ### Ejercicio 18

paresDeNat :: [(Int, Int)]
paresDeNat = [(i, k - i) | k <- [0 ..], i <- [0 .. k]]

--- ### Ejercicio 19

--- _Una tripla pitagÃ³rica es una tripla (a, b, c) de enteros positivos tal que
--- $a^2 + b^2 = c^2$. La siguiente expresiÃ³n intenta ser una definiciÃ³n de una
--- lista (infinita) de triplas pitagÃ³ricas:_

pitagÃ³ricas :: [(Integer, Integer, Integer)]
pitagÃ³ricas =
  [(a, b, c) | a <- [1 ..], b <- [1 ..], c <- [1 ..], a ^ 2 + b ^ 2 == c ^ 2]

--- _Explicar por quÃ© esta definiciÃ³n no es Ãºtil. Dar una definiciÃ³n mejor._

--- Esta definiciÃ³n no es Ãºtil porque genera todas las (infinitas) triplas que
--- empiezan con 1 primero, y por lo tanto nunca se llegarÃ¡n a generar otras. Se
--- podrÃ­a definir la funciÃ³n de acuerdo a $c$ usando la desigualdad triangular:

pitagoricas' :: [(Integer, Integer, Integer)]
pitagoricas' =
  [(a, b, c) | c <- [1 ..], a <- [1 .. c], b <- [1 .. c], a ^ 2 + b ^ 2 == c ^ 2]

--- ### Ejercicio 20

--- Escribir la funciÃ³n `listasQueSuman :: Int -> [[Int]]` que, dado un nÃºmero
--- natural $n$, devuelve todas las listas de enteros positivos (es decir,
--- mayores o iguales que 1) cuya suma sea $n$. Para este ejercicio se permite
--- usar recursiÃ³n explÃ­cita.

listasQueSuman :: Int -> [[Int]]
listasQueSuman 0 = [[]]
listasQueSuman n = [x : l | x <- [1 .. n], l <- listasQueSuman (n - x)]

--- ### Ejercicio 21

--- Definir en Haskell una lista que contenga todas las listas finitas de
--- enteros positivos (esto es, con elementos mayores o iguales que 1).

listasFinitasDeNat :: [[Int]]
listasFinitasDeNat = [l | i <- [0 ..], l <- listasQueSuman i]

--- ### Ejercicio 23

--- _Dado el tipo de datos `AIH a` definido en el ejercicio 14:_

--- a) _Definir la lista (infinita) de todos los AIH cuyas hojas tienen tipo ().
---    Se recomienda definir una funciÃ³n auxiliar. Para este ejercicio se
---    permite utilizar recursiÃ³n explÃ­cita._

todosAIH :: [AIH ()]
todosAIH = [a | i <- [1 ..], a <- conAltura i]
 where
  conAltura 1 = [Hoja ()]
  conAltura 2 = [Bin' (Hoja ()) (Hoja ())]
  conAltura n =
    [Bin' l r | l <- conAltura (n - 1), r <- conAltura (n - 1)]
      ++ [Bin' (Hoja ()) t | t <- conAltura (n - 1)]
      ++ [Bin' t (Hoja ()) | t <- conAltura (n - 1)]

--- b) _Explicar por quÃ© la recursiÃ³n utilizada en el punto a) no es estructural._

--- qsy (?)